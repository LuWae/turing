# TODO

main {
    unpop(unpop(umul(accept)))
}

l(E) {
  [def] < E
}

r(E) {
  [def] > E
}

p($a, E) {
  [def] =$a E
}

fl($a, E) {
  [$a] E
  [def] <
}

fr($a, E) {
  [$a] E
  [def] >
}

lfl($a, E) {
  [def] < fl($a, E)
}

rfr($a, E) {
  [def] > fr($a, E)
}

fl2($a, $b, E) {
  [$a, $b] E
  [def] <
}

fr2($a, $b, E) {
  [$a, $b] E
  [def] >
}

lfl2($a, $b, E) {
  [def] < fl2($a, $b, E)
}

rfr2($a, $b, E) {
  [def] > fr2($a, $b, E)
}

frcascade($a, $b, E) {
  [$a] E
  [def] =$b >
}

if($a, A, E) {
  [$a] A
  [def] E
}

if2($a, A, $b, B, E) {
  [$a] A
  [$b] B
  [def] E
}

push($a, $b, $c, $d, $e, $f, $g, $h, E) {
    [def] ='#' > =$a > =$b > =$c > =$d > =$e > =$f > =$g > =$h > ='#' E
}

inc(E) {
    [def] < if2(
        '0', p('1', fr('#', E)),
        '1', p('0', inc(E)),
        rfr('#', E)
      )
}

dec(E, D) {
    [def] < if2(
        '0', dec(E, D),
        '1', p('0', r(frcascade('#', '1', E))),
        rfr('#', D)
      )
}

subst(E) {
    ['0'] ='a' E
    ['1'] ='b' E
    [def] E
}

resubst(E) {
    ['a'] ='0' E
    ['b'] ='1' E
    [def] E
}

add(E) {
  [def] lfl('#', l(subst(r(rfr('#', l(add0(E)))))))
}

add0(E) {
  ['0'] ='a' lfl2('a', 'b', resubst(l(add2(E))))
  ['1'] ='b' lfl2('a', 'b', add1(E))
  [def] E
}

add1(E) {
  ['a'] ='1' < add2(E)
  ['b'] ='0' < add3(E)
}

add2(E) {
  ['#'] fr2('a', 'b', resubst(fl('#', E)))
  [def] subst(rfr2('a', 'b', resubst(l(add0(E)))))
}

add3(E) {
  ['0'] ='b' rfr2('a', 'b', resubst(l(add0(E))))
  ['1'] ='a' < add4(E)
  [def] fr2('a', 'b', resubst(fl('#', E)))
}

add4(E) {
  ['0'] ='1' add5(E)
  ['1'] ='0' < add4(E)
  [def] add5(E)
}

add5(E) {
  [def] fr2('a', 'b', rfr2('a', 'b', resubst(l(add0(E)))))
}

not(E) {
  [def] < if2(
    '0', p('1', not(E)),
    '1', p('0', not(E)),
    rfr('#', E)
  )
}

neg(E) {
  [def] not(inc(E))
}

sub(E) {
  [def] neg(add(E))
}

pop(E) {
  [def] lfl('#', E)
}

unpop(E) {
  [def] rfr('#', E)
}

ifzero(E, D) {
  [def] < if2(
    '0', l(ifzero(E, D)),
    '1', fr('#', D),
    rfr('#', E)
  )
}

ifnzero(E, D) {
  [def] ifzero(D, E)
}

ifneg(E, D) {
  [def] lfl('#', r(
    if('1', fr('#', E), fr('#', D))
  ))
}

abs(E) {
    [def] ifneg(neg(E), E)
}

dup(E) {
  [def] > ='a' < lfl('#', r(dup1(E)))
}

dup1(E) {
  ['0'] ='a' dup2('0', E)
  ['1'] ='b' dup2('1', E)
  [def] fr('a', p('#', E))
}

dup2($a, E) {
  [def] rfr('a', p($a, r(p('a', lfl2('a', 'b', resubst(r(dup1(E))))))))
}

umul(E) {
  [def] dup(pop(pop(dup(pop(pop(r(frcascade('#', '0', unpop(unpop(umul1(E)))))))))))
}

umul1(E) {
  [def] dec(
    pop(add(unpop(unpop(umul1(E))))),
    pop(pop(E))
  )
}
